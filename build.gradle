plugins {
    id 'com.github.spotbugs' version '1.6.0'
}

allprojects {
    repositories {
        jcenter()                               // JDA, maybe others
        mavenCentral()                          // everything else
        mavenLocal()                            // local maven repo, mostly for testing stuff
        maven { url 'https://jitpack.io' }      // for getting builds from github
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.spotbugs'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group 'space.npstr'
    version '0.0.2'

    ext {
        gradleVers = '4.2.1'

        spotbugsVersion = '3.1.0-RC7'
        postgresDriverVersion = '42.1.4'
        hibernateVersion = '5.2.12.Final'
        hikariVersion = '2.7.2'
        jschVersion = '0.1.54'
        slf4jVersion = '1.7.25'
        prometheusVersion = '0.1.0'

        //discord entities module
        jdaVersion = '3.3.1_295'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId rootProject.group
                artifactId moduleName

                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }

    compileJava.dependsOn 'clean'
    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

    jar.dependsOn 'spotbugsMain'
    task install(dependsOn: 'publishToMavenLocal')
    publishToMavenLocal.dependsOn 'jar'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }
}

subprojects {
    dependencies {
        compileOnly rootProject
    }
}

ext {
    moduleName = 'sqlsauce'
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleVers
    //noinspection UnnecessaryQualifiedReference
    distributionType = org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL
}

dependencies {
    //database stuff
    compile group: 'org.postgresql', name: 'postgresql', version: postgresDriverVersion //postgres jdbc driver
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion   //hibernate + JPA
    compile group: 'com.zaxxer', name: 'HikariCP', version: hikariVersion               //connection pool

    //various/QA
    compile group: 'com.jcraft', name: 'jsch', version: jschVersion           //ssh tunnel
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion      //logging framework
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: spotbugsVersion //annotations
    compileOnly group: 'io.prometheus', name: 'simpleclient_hibernate', version: prometheusVersion   //db metrics
}
