plugins {
    id 'com.github.spotbugs' version '1.6.0'
}

allprojects {
    repositories {
        jcenter()                               // JDA, maybe others
        mavenCentral()                          // everything else
        mavenLocal()                            // local maven repo, mostly for testing stuff
        maven { url 'https://jitpack.io' }      // for getting builds from github
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.spotbugs'

    sourceCompatibility = 9
    targetCompatibility = 9

    group 'space.npstr'
    version '0.0.3-SNAPSHOT'

    ext {
        gradleVers = '4.3'

        postgresDriverVersion = '42.1.4'
        hibernateVersion = '5.2.12.Final'
        hikariVersion = '2.7.2'
        jschVersion = '0.1.54'

        jaxbApiVersion = '2.3.0'
        slf4jApiVersion = '1.8.0-beta0'
        spotbugsVersion = '3.1.0'
        prometheusVersion = '0.1.0'

        //discord entities module
        jdaVersion = '3.3.1_300'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId rootProject.group
                artifactId moduleName

                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }

    compileJava.dependsOn 'clean'
    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

    jar.dependsOn 'spotbugsMain'
    task install(dependsOn: 'publishToMavenLocal')
    publishToMavenLocal.dependsOn 'jar'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }
}

subprojects {
    dependencies {
        compileOnly rootProject
    }
}

ext {
    moduleName = 'sqlsauce'
}

import org.gradle.api.tasks.wrapper.Wrapper.DistributionType

task wrapper(type: Wrapper) {
    gradleVersion = gradleVers
    distributionType = DistributionType.ALL
}

dependencies {
    //database & connection stuff
    compile group: 'org.postgresql', name: 'postgresql', version: postgresDriverVersion //postgres jdbc driver
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion   //hibernate + JPA
    compile group: 'com.zaxxer', name: 'HikariCP', version: hikariVersion               //connection pool
    compile group: 'com.jcraft', name: 'jsch', version: jschVersion                     //ssh tunnel

    //various/QA
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbApiVersion    //required by hibernate for java 9
    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: slf4jApiVersion   //logging framework
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: spotbugsVersion //annotations
    compileOnly group: 'io.prometheus', name: 'simpleclient_hibernate', version: prometheusVersion   //db metrics
}
